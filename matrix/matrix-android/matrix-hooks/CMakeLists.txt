cmake_minimum_required(VERSION 3.4.1)

option(EnableLOG "Enable QUT Logs" ON)
if(EnableLOG)
    add_definitions(-DEnableLOG)
endif()

set(SOURCE_DIR src/main/cpp)

find_library(log-lib, log)

add_subdirectory(${SOURCE_DIR}/external/libcJSON)

################################## Common Part ##################################
set(TARGET matrix-hookcommon)

add_library(
      ${TARGET}
      SHARED
      ${SOURCE_DIR}/common/JNICommon.cpp
      ${SOURCE_DIR}/common/HookCommon.cpp
      ${SOURCE_DIR}/common/ReentrantPrevention.cpp
)

target_include_directories(
      ${TARGET}
      PUBLIC ${SOURCE_DIR}
      PUBLIC ${EXT_DEP}/include
)

target_compile_options(
      ${TARGET}
      PUBLIC $<$<COMPILE_LANGUAGE:C>:-std=c99>
      PUBLIC $<$<COMPILE_LANGUAGE:CXX>:-std=c++17 -fno-exceptions -fno-rtti>
      PUBLIC -fdata-sections -ffunction-sections -fvisibility=hidden -fstack-protector
)

target_link_libraries(
      ${TARGET}
      PUBLIC -Wl,--gc-sections
      PUBLIC cJSON
      PUBLIC ${log-lib}
      PUBLIC ${EXT_DEP}/lib/${ANDROID_ABI}/libwechatbacktrace.so
      PRIVATE ${EXT_DEP}/lib/${ANDROID_ABI}/libxhook.a
)
#################################################################################

################################## Memory Hook ##################################
set(TARGET matrix-memoryhook)

add_library(
        ${TARGET}
        SHARED
        ${SOURCE_DIR}/memory/MemoryHookJNI.cpp
        ${SOURCE_DIR}/memory/MemoryHook.cpp
        ${SOURCE_DIR}/memory/MemoryHookFunctions.cpp
)

target_link_libraries(
        ${TARGET}
        PRIVATE matrix-hookcommon
        PRIVATE ${EXT_DEP}/lib/${ANDROID_ABI}/libxhook.a
)
#################################################################################

################################# Pthread Hook ##################################
set(TARGET matrix-pthreadhook)

add_library(
      ${TARGET}
      SHARED
      ${SOURCE_DIR}/pthread/ThreadTrace.cpp
      ${SOURCE_DIR}/pthread/ThreadStackShink.cpp
      ${SOURCE_DIR}/pthread/PthreadHook.cpp
      ${SOURCE_DIR}/pthread/PthreadHookJNI.cpp
)

target_link_libraries(
      ${TARGET}
      PRIVATE matrix-hookcommon
      PRIVATE ${EXT_DEP}/lib/${ANDROID_ABI}/libxhook.a
)
#################################################################################

################################### EGL Hook ####################################
# TODO ??
#################################################################################